 <program> -> <global-dec> <func-def> shimenet kween ( ) { <kween-body> } <func-def> 
 <global-dec> -> <var-dec-init> <global-dec> 
 <global-dec> -> λ 
 <var-dec-init> -> <naur-case> <data-type> id <var-dec-init-tail> ; 
 <var-dec-init> -> λ 
 <naur-case> -> naur 
 <naur-case> -> λ 
 <return-type> -> <data-type> 
 <return-type> -> shimenet 
 <data-type>  -> anda 
 <data-type>  -> andamhie 
 <data-type>  -> chika 
 <data-type>  -> eklabool 
 <var-dec-init-tail> -> <func-case> 
 <var-dec-init-tail> -> <array-case> <var-init> 
 <var-dec-init-tail> -> λ 
 <func-case> -> ( <arguments> ) 
 <arguments> -> <func-arguments> <multi-arguments> 
 <arguments> -> λ 
 <func-arguments> -> <argument-type> id  
 <argument-type> -> <data-type> 
 <argument-type> -> λ 
 <multi-arguments> -> , <func-arguments> <multi-arguments> 
 <multi-arguments> -> λ 
 <array-case> -> [ <array-values> ] <2d-index>  
 <array-case> -> λ 
 <2d-index> -> [ <array-values> ] <3d-index> 
 <2d-index> -> λ 
 <3d-index>  -> [ <array-values> ] 
 <3d-index>  -> λ 
 <array-values> -> <expression-operands> 
 <literals> -> anda_literal 
 <literals> -> andamhie_literal 
 <literals> -> chika_literal 
 <literals> -> <eklabool> 
 <eklabool> -> korik 
 <eklabool> -> eme 
 <func-array>  -> <func-array-value> 
 <func-array>   -> λ 
 <func-array-value> -> ( <arguments> ) 
 <func-array-value> -> <array-case> 
 <var-init>  -> = <init-values> <multi-init-values>  
 <var-init>  -> λ 
 <multi-init-values> -> , id <array-case> = <init-values> <multi-init-values>  
 <multi-init-values> -> λ 
 <init-values> -> { <array-elements> } 
 <init-values> -> <expression> 
 <array-elements> -> <array-values> <extra-array-value> 
 <array-elements> -> { <2d-array-elements> } <extra-2d-array-value>  
 <2d-array-elements> -> <array-values> <extra-array-value> 
 <2d-array-elements> -> { <array-values> <extra-array-value> } <extra-2d-array-value> 
 <2d-array-elements> -> λ 
 <extra-array-value> -> , <array-values> <extra-array-value> 
 <extra-array-value> -> λ 
 <extra-2d-array-value> -> , { <extra-3d-array-value> } <extra-2d-array-value> 
 <extra-2d-array-value> -> λ 
 <extra-3d-array-value> -> <array-values> <extra-array-value> 
 <extra-3d-array-value> -> { <array-values> <extra-array-value> } <extra-2d-array-value> 
 <extra-3d-array-value> -> λ 
 <func-def> -> <return-type> id ( <parameters> ) { <func-body> } <func-def> 
 <func-def> -> λ 
 <parameters> -> <arguments> 
 <func-body> -> <local-dec> <statements> <multi-statements> 
 <local-dec> -> <var-dec-init> <array-assign> <local-dec> 
 <local-dec> -> λ 
 <func-assign> -> id = <func-call> 
 <func-call> -> id ( <arguments> ) 
 <array-assign> -> id <array-case> = <array-values> ; 
 <array-assign> -> λ 
 <kween-body> -> <func-body> 
 <expression> -> <simple-expression> 
 <expression> -> <multi-expression> 
 <simple-expression> -> <expression-operands> <expression-tail> 
 <multi-expression> -> ( <expression> ) <more-expression> 
 <more-expression> -> <general-operators> <expression-operands> <more-expression> 
 <more-expression> -> λ 
 <expression-tail> -> <general-operators> <expression-operands> <more-expression> 
 <expression-tail> -> λ 
 <expression-operands> -> <negative-value> <expression-value> 
 <negative-value> -> - 
 <negative-value> -> λ 
 <expression-value> -> <unary-operator> id  
 <expression-value> -> id <identifier-tail> 
 <expression-value> -> <general-operands> 
 <identifier-tail> -> <func-array> 
 <identifier-tail> -> <unary-operator> 
 <identifier-tail> -> λ 
 <unary-operator> -> -- 
 <unary-operator> -> ++ 
 <general-operands> -> <literals> 
 <general-operators> -> + 
 <general-operators> -> += 
 <general-operators> -> - 
 <general-operators> -> % 
 <general-operators> -> / 
 <general-operators> -> *  
 <general-operators> -> ** 
 <general-operators> -> // 
 <general-operators> -> >  
 <general-operators> -> <  
 <general-operators> -> >= 
 <general-operators> -> <= 
 <general-operators> -> == 
 <general-operators> -> != 
 <general-operators> -> = 
 <general-operators> -> && 
 <general-operators> -> || 
 <general-operators> -> ! 
 <statements> -> <assign-stmts> <statements> 
 <statements> -> <input-stmts> <statements> 
 <statements> -> <output-stmts> <statements> 
 <statements> -> <conditional-stmts> <statements> 
 <statements> -> <loop-stmts> <statements> 
 <statements> -> <switch-stmts> <statements> 
 <statements> -> <control-flow-stmts> <statements> 
 <statements> -> <return-stmts> <statements> 
 <statements> -> λ 
 <assign-stmts> -> id <arithmetic-operators> = <assignment-values> ; 
 <arithmetic-operators> -> + 
 <arithmetic-operators> -> - 
 <arithmetic-operators> -> % 
 <arithmetic-operators> -> / 
 <arithmetic-operators> -> // 
 <arithmetic-operators> -> * 
 <arithmetic-operators> -> ** 
 <assignment-values> -> <expression-operands> 
 <input-stmts> -> <input-type> id = givenchy ( <givenchy-values> ) ; 
 <input-type> -> <data-type> 
 <input-type> -> λ 
 <givenchy-values> -> <expression-operands> 
 <output-stmts> -> serve ( <output-values> ) ; 
 <output-values> -> <expression-operands> 
 <conditional-stmts> -> pak ( <condition> ) { <conditional-body> } <ganern-pak-statement> <ganern-case> 
 <condition> -> <expression-operands> 
 <conditional-body> -> <local-dec> <statements> <multi-statements> 
 <ganern-pak-statement> -> ganern pak ( <condition> ) { <conditional-body> } <ganern-pak-statement> 
 <ganern-pak-statement> -> λ 
 <ganern-case> -> ganern { <conditional-body> } 
 <ganern-case> -> λ 
 <loop-stmts> -> <forda-statement> 
 <loop-stmts> -> <keri-statement> 
 <forda-statement> -> forda ( <loop-type-init> id from <start-value> to <end-value> <step-case> ) { <for-loop-body> } 
 <loop-type-init> -> <data-type> 
 <loop-type-init> -> λ 
 <start-value> -> <loop-conditions> 
 <end-value> -> <loop-conditions> 
 <step-case> -> step <update> 
 <step-case> -> λ 
 <loop-conditions> -> <expression-operands> 
  <for-loop-body> -> <local-dec> <statements> <multi-statements> 
 <keri-statement> -> keri <keri-case>  
 <keri-case> -> ( <condition> ) { <while-loop-body> } 
 <keri-case> -> lang { <while-loop-body> } keri ( <condition> ) ; 
 <while-loop-body> -> <local-dec> <statements> <multi-statements> 
 <switch-stmts> -> versa ( <switch-values> ) {betsung <switch-values> : <switch-statements> <amaccana-case> ; <multi-cases> <ditech-case> <switch-statements> } 
 <multi-cases> -> betsung <switch-values> : <switch-statements> <amaccana-case> ; <multi-cases> 
 <multi-cases> -> λ 
 <switch-values> -> <expression-operands> 
 <switch-statements> -> <local-dec> <statements> <multi-statements> 
 <amaccana-case> -> amaccana 
 <amaccana-case> -> λ 
 <ditech-case> -> ditech : 
 <ditech-case> -> λ 
 <control-flow-stmts> -> gogogo ; 
 <control-flow-stmts> -> amaccana ; 
 <return-stmts> -> push <push-values> ;    
 <return-stmts> -> λ 
 <push-values> -> <expression> 
 <push-values> -> λ 

